plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.github.spotbugs' version '5.0.14'
    id 'jacoco'
    id 'org.sonarqube' version '4.4.0'
}

group = 'com.example'
version = '0.1.0'
sourceCompatibility = '21'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'

    testImplementation 'org.seleniumhq.selenium:selenium-java:4.11.0'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.5.1'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
    }
}


spotbugs {
    toolVersion = "4.8.0" 
    ignoreFailures = true
    effort = "max"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


sonarqube {
    properties {
        property "sonar.host.url", System.getProperty("sonar.host.url", "https://sonarcloud.io")
        property "sonar.organization", System.getProperty("sonar.organization", "")
        property "sonar.projectKey", System.getProperty("sonar.projectKey", "")
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"

    }
}
